// Copyright 2022 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

/*
  Floating-point error function and complementary error function.
*/

// The Jule code is a modified version of the original Go code from
// https://github.com/golang/go/blob/go1.19/src/math/erf.go and came with this notice.
//
// ====================================================
// Copyright (c) 2009 The Go Authors. All rights reserved.
// 
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are
// met:
// 
//    * Redistributions of source code must retain the above copyright
// notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above
// copyright notice, this list of conditions and the following disclaimer
// in the documentation and/or other materials provided with the
// distribution.
//    * Neither the name of Google Inc. nor the names of its
// contributors may be used to endorse or promote products derived from
// this software without specific prior written permission.
// 
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
// "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
// LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
// A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
// OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
// SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
// LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
// DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
// THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
// OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// ====================================================

// The original C code and the long comment below are
// from FreeBSD's /usr/src/lib/msun/src/s_erf.c and
// came with this notice. The Jule code is a simplified
// version of the original C.
//
// ====================================================
// Copyright (C) 1993 by Sun Microsystems, Inc. All rights reserved.
//
// Developed at SunPro, a Sun Microsystems, Inc. business.
// Permission to use, copy, modify, and distribute this
// software is freely granted, provided that this notice
// is preserved.
// ====================================================
//
//
// double erf(double x)
// double erfc(double x)
//                           x
//                    2      |\
//     erf(x)  =  ---------  | exp(-t*t)dt
//                 sqrt(pi) \|
//                           0
//
//     erfc(x) =  1-erf(x)
//  Note that
//              erf(-x) = -erf(x)
//              erfc(-x) = 2 - erfc(x)
//
// Method:
//      1. For |x| in [0, 0.84375]
//          erf(x)  = x + x*R(x**2)
//          erfc(x) = 1 - erf(x)           if x in [-.84375,0.25]
//                  = 0.5 + ((0.5-x)-x*R)  if x in [0.25,0.84375]
//         where R = P/Q where P is an odd poly of degree 8 and
//         Q is an odd poly of degree 10.
//                                               -57.90
//                      | R - (erf(x)-x)/x | <= 2
//
//
//         Remark. The formula is derived by noting
//          erf(x) = (2/sqrt(pi))*(x - x**3/3 + x**5/10 - x**7/42 + ....)
//         and that
//          2/sqrt(pi) = 1.128379167095512573896158903121545171688
//         is close to one. The interval is chosen because the fix
//         point of erf(x) is near 0.6174 (i.e., erf(x)=x when x is
//         near 0.6174), and by some experiment, 0.84375 is chosen to
//         guarantee the error is less than one ulp for erf.
//
//      2. For |x| in [0.84375,1.25], let s = |x| - 1, and
//         c = 0.84506291151 rounded to single (24 bits)
//              erf(x)  = sign(x) * (c  + P1(s)/Q1(s))
//              erfc(x) = (1-c)  - P1(s)/Q1(s) if x > 0
//                        1+(c+P1(s)/Q1(s))    if x < 0
//              |P1/Q1 - (erf(|x|)-c)| <= 2**-59.06
//         Remark: here we use the taylor series expansion at x=1.
//              erf(1+s) = erf(1) + s*Poly(s)
//                       = 0.845.. + P1(s)/Q1(s)
//         That is, we use rational approximation to approximate
//                      erf(1+s) - (c = (single)0.84506291151)
//         Note that |P1/Q1|< 0.078 for x in [0.84375,1.25]
//         where
//              P1(s) = degree 6 poly in s
//              Q1(s) = degree 6 poly in s
//
//      3. For x in [1.25,1/0.35(~2.857143)],
//              erfc(x) = (1/x)*exp(-x*x-0.5625+R1/S1)
//              erf(x)  = 1 - erfc(x)
//         where
//              R1(z) = degree 7 poly in z, (z=1/x**2)
//              S1(z) = degree 8 poly in z
//
//      4. For x in [1/0.35,28]
//              erfc(x) = (1/x)*exp(-x*x-0.5625+R2/S2) if x > 0
//                      = 2.0 - (1/x)*exp(-x*x-0.5625+R2/S2) if -6<x<0
//                      = 2.0 - tiny            (if x <= -6)
//              erf(x)  = sign(x)*(1.0 - erfc(x)) if x < 6, else
//              erf(x)  = sign(x)*(1.0 - tiny)
//         where
//              R2(z) = degree 6 poly in z, (z=1/x**2)
//              S2(z) = degree 7 poly in z
//
//      Note1:
//         To compute exp(-x*x-0.5625+R/S), let s be a single
//         precision number and s := x; then
//              -x*x = -s*s + (s-x)*(s+x)
//              exp(-x*x-0.5626+R/S) =
//                      exp(-s*s-0.5625)*exp((s-x)*(s+x)+R/S);
//      Note2:
//         Here 4 and 5 make use of the asymptotic series
//                        exp(-x*x)
//              erfc(x) ~ ---------- * ( 1 + Poly(1/x**2) )
//                        x*sqrt(pi)
//         We use rational approximation to approximate
//              g(s)=f(1/x**2) = log(erfc(x)*x) - x*x + 0.5625
//         Here is the error bound for R1/S1 and R2/S2
//              |R1/S1 - f(x)|  < 2**(-62.57)
//              |R2/S2 - f(x)|  < 2**(-61.52)
//
//      5. For inf > x >= 28
//              erf(x)  = sign(x) *(1 - tiny)  (raise inexact)
//              erfc(x) = tiny*tiny (raise underflow) if x > 0
//                      = 2 - tiny if x<0
//
//      7. Special case:
//              erf(0)  = 0, erf(inf)  = 1, erf(-inf) = -1,
//              erfc(0) = 1, erfc(inf) = 0, erfc(-inf) = 2,
//              erfc/erf(NaN) is NaN

const ERX = 8.45062911510467529297e-01 // 0x3FEB0AC160000000
// Coefficients for approximation to  erf in [0, 0.84375]
const EFX  = 1.28379167095512586316e-01  // 0x3FC06EBA8214DB69
const EFX8 = 1.02703333676410069053e+00  // 0x3FF06EBA8214DB69
const PP0  = 1.28379167095512558561e-01  // 0x3FC06EBA8214DB68
const PP1  = -3.25042107247001499370e-01 // 0xBFD4CD7D691CB913
const PP2  = -2.84817495755985104766e-02 // 0xBF9D2A51DBD7194F
const PP3  = -5.77027029648944159157e-03 // 0xBF77A291236668E4
const PP4  = -2.37630166566501626084e-05 // 0xBEF8EAD6120016AC
const QQ1  = 3.97917223959155352819e-01  // 0x3FD97779CDDADC09
const QQ2  = 6.50222499887672944485e-02  // 0x3FB0A54C5536CEBA
const QQ3  = 5.08130628187576562776e-03  // 0x3F74D022C4D36B0F
const QQ4  = 1.32494738004321644526e-04  // 0x3F215DC9221C1A10
const QQ5  = -3.96022827877536812320e-06 // 0xBED09C4342A26120
// Coefficients for approximation to  erf  in [0.84375, 1.25]
const PA0 = -2.36211856075265944077e-03 // 0xBF6359B8BEF77538
const PA1 = 4.14856118683748331666e-01  // 0x3FDA8D00AD92B34D
const PA2 = -3.72207876035701323847e-01 // 0xBFD7D240FBB8C3F1
const PA3 = 3.18346619901161753674e-01  // 0x3FD45FCA805120E4
const PA4 = -1.10894694282396677476e-01 // 0xBFBC63983D3E28EC
const PA5 = 3.54783043256182359371e-02  // 0x3FA22A36599795EB
const PA6 = -2.16637559486879084300e-03 // 0xBF61BF380A96073F
const QA1 = 1.06420880400844228286e-01  // 0x3FBB3E6618EEE323
const QA2 = 5.40397917702171048937e-01  // 0x3FE14AF092EB6F33
const QA3 = 7.18286544141962662868e-02  // 0x3FB2635CD99FE9A7
const QA4 = 1.26171219808761642112e-01  // 0x3FC02660E763351F
const QA5 = 1.36370839120290507362e-02  // 0x3F8BEDC26B51DD1C
const QA6 = 1.19844998467991074170e-02  // 0x3F888B545735151D
// Coefficients for approximation to  erfc in [1.25, 1/0.35]
const RA0 = -9.86494403484714822705e-03 // 0xBF843412600D6435
const RA1 = -6.93858572707181764372e-01 // 0xBFE63416E4BA7360
const RA2 = -1.05586262253232909814e+01 // 0xC0251E0441B0E726
const RA3 = -6.23753324503260060396e+01 // 0xC04F300AE4CBA38D
const RA4 = -1.62396669462573470355e+02 // 0xC0644CB184282266
const RA5 = -1.84605092906711035994e+02 // 0xC067135CEBCCABB2
const RA6 = -8.12874355063065934246e+01 // 0xC054526557E4D2F2
const RA7 = -9.81432934416914548592e+00 // 0xC023A0EFC69AC25C
const SA1 = 1.96512716674392571292e+01  // 0x4033A6B9BD707687
const SA2 = 1.37657754143519042600e+02  // 0x4061350C526AE721
const SA3 = 4.34565877475229228821e+02  // 0x407B290DD58A1A71
const SA4 = 6.45387271733267880336e+02  // 0x40842B1921EC2868
const SA5 = 4.29008140027567833386e+02  // 0x407AD02157700314
const SA6 = 1.08635005541779435134e+02  // 0x405B28A3EE48AE2C
const SA7 = 6.57024977031928170135e+00  // 0x401A47EF8E484A93
const SA8 = -6.04244152148580987438e-02 // 0xBFAEEFF2EE749A62
// Coefficients for approximation to  erfc in [1/.35, 28]
const RB0 = -9.86494292470009928597e-03 // 0xBF84341239E86F4A
const RB1 = -7.99283237680523006574e-01 // 0xBFE993BA70C285DE
const RB2 = -1.77579549177547519889e+01 // 0xC031C209555F995A
const RB3 = -1.60636384855821916062e+02 // 0xC064145D43C5ED98
const RB4 = -6.37566443368389627722e+02 // 0xC083EC881375F228
const RB5 = -1.02509513161107724954e+03 // 0xC09004616A2E5992
const RB6 = -4.83519191608651397019e+02 // 0xC07E384E9BDC383F
const SB1 = 3.03380607434824582924e+01  // 0x403E568B261D5190
const SB2 = 3.25792512996573918826e+02  // 0x40745CAE221B9F0A
const SB3 = 1.53672958608443695994e+03  // 0x409802EB189D5118
const SB4 = 3.19985821950859553908e+03  // 0x40A8FFB7688C246A
const SB5 = 2.55305040643316442583e+03  // 0x40A3F219CEDF3BE6
const SB6 = 4.74528541206955367215e+02  // 0x407DA874E79FE763
const SB7 = -2.24409524465858183362e+01 // 0xC03670E242712D62

// Returns the error function of x.
//
// Special cases are:
//  erf(inf) = 1
//  erf(-inf) = -1
//  erf(nan) = nan
pub fn erf(mut x: f64): f64 {
	const VERY_TINY = 2.848094538889218e-306 // 0x0080000000000000
	const SMALL     = 1.0 / (1 << 28)        // 2**-28
	// special cases
	match {
	| is_nan(x):
		ret nan()
	| is_inf(x, 1):
		ret 1
	| is_inf(x, -1):
		ret -1
	}
	let mut sign = false
	if x < 0 {
		x = -x
		sign = true
	}
	if x < 0.84375 { // |x| < 0.84375
		let mut temp: f64 = 0
		if x < SMALL { // |x| < 2**-28
			if x < VERY_TINY {
				temp = 0.125 * (8.0*x + EFX8*x) // avoid underflow
			} else {
				temp = x + EFX*x
			}
		} else {
			let z = x * x
			let r = PP0 + z*(PP1+z*(PP2+z*(PP3+z*PP4)))
			let s = 1 + z*(QQ1+z*(QQ2+z*(QQ3+z*(QQ4+z*QQ5))))
			let y = r / s
			temp = x + x*y
		}
		if sign {
			ret -temp
		}
		ret temp
	}
	if x < 1.25 { // 0.84375 <= |x| < 1.25
		let s = x - 1
		let P = PA0 + s*(PA1+s*(PA2+s*(PA3+s*(PA4+s*(PA5+s*PA6)))))
		let Q = 1 + s*(QA1+s*(QA2+s*(QA3+s*(QA4+s*(QA5+s*QA6)))))
		if sign {
			ret -ERX - P/Q
		}
		ret ERX + P/Q
	}
	if x >= 6 { // inf > |x| >= 6
		if sign {
			ret -1
		}
		ret 1
	}
	let s = 1 / (x * x)
	let mut R: f64 = 0
	let mut S: f64 = 0
	if x < 1/0.35 { // |x| < 1 / 0.35  ~ 2.857143
		R = RA0 + s*(RA1+s*(RA2+s*(RA3+s*(RA4+s*(RA5+s*(RA6+s*RA7))))))
		S = 1 + s*(SA1+s*(SA2+s*(SA3+s*(SA4+s*(SA5+s*(SA6+s*(SA7+s*SA8)))))))
	} else { // |x| >= 1 / 0.35  ~ 2.857143
		R = RB0 + s*(RB1+s*(RB2+s*(RB3+s*(RB4+s*(RB5+s*RB6)))))
		S = 1 + s*(SB1+s*(SB2+s*(SB3+s*(SB4+s*(SB5+s*(SB6+s*SB7))))))
	}
	let z = f64_from_bits(f64_bits(x) & 0xffffffff00000000) // pseudo-single (20-bit) precision x
	let r = exp(-z*z-0.5625) * exp((z-x)*(z+x)+R/S)
	if sign {
		ret r/x - 1
	}
	ret 1 - r/x
}

// Returns the complementary error function of x.
//
// Special cases are:
//  erfc(inf) = 0
//  erfc(-inf) = 2
//  erfc(nan) = nan
pub fn erfc(mut x: f64): f64 {
	const TINY = 1.0 / (1 << 56) // 2**-56
	// special cases
	match {
	| is_nan(x):
		ret nan()
	| is_inf(x, 1):
		ret 0
	| is_inf(x, -1):
		ret 2
	}
	let mut sign = false
	if x < 0 {
		x = -x
		sign = true
	}
	if x < 0.84375 { // |x| < 0.84375
		let mut temp: f64 = 0
		if x < TINY { // |x| < 2**-56
			temp = x
		} else {
			let z = x * x
			let r = PP0 + z*(PP1+z*(PP2+z*(PP3+z*PP4)))
			let s = 1 + z*(QQ1+z*(QQ2+z*(QQ3+z*(QQ4+z*QQ5))))
			let y = r / s
			if x < 0.25 { // |x| < 1/4
				temp = x + x*y
			} else {
				temp = 0.5 + (x*y + (x - 0.5))
			}
		}
		if sign {
			ret 1 + temp
		}
		ret 1 - temp
	}
	if x < 1.25 { // 0.84375 <= |x| < 1.25
		let s = x - 1
		let P = PA0 + s*(PA1+s*(PA2+s*(PA3+s*(PA4+s*(PA5+s*PA6)))))
		let Q = 1 + s*(QA1+s*(QA2+s*(QA3+s*(QA4+s*(QA5+s*QA6)))))
		if sign {
			ret 1 + ERX + P/Q
		}
		ret 1 - ERX - P/Q

	}
	if x < 28 { // |x| < 28
		let s = 1 / (x * x)
		let mut R: f64 = 0
		let mut S: f64 = 0
		if x < 1/0.35 { // |x| < 1 / 0.35 ~ 2.857143
			R = RA0 + s*(RA1+s*(RA2+s*(RA3+s*(RA4+s*(RA5+s*(RA6+s*RA7))))))
			S = 1 + s*(SA1+s*(SA2+s*(SA3+s*(SA4+s*(SA5+s*(SA6+s*(SA7+s*SA8)))))))
		} else { // |x| >= 1 / 0.35 ~ 2.857143
			if sign && x > 6 {
				ret 2 // x < -6
			}
			R = RB0 + s*(RB1+s*(RB2+s*(RB3+s*(RB4+s*(RB5+s*RB6)))))
			S = 1 + s*(SB1+s*(SB2+s*(SB3+s*(SB4+s*(SB5+s*(SB6+s*SB7))))))
		}
		let z = f64_from_bits(f64_bits(x) & 0xffffffff00000000) // pseudo-single (20-bit) precision x
		let r = exp(-z*z-0.5625) * exp((z-x)*(z+x)+R/S)
		if sign {
			ret 2 - r/x
		}
		ret r / x
	}
	if sign {
		ret 2
	}
	ret 0
}
